class Environment
{
	
	// Average day and night temperatures - 0J 1F 2M 3A 4M 5J 6J 7A 8S 9O 10N 11D
	protected float	   dayTemperatures[12]={ 1, 3, 5,9,11,20,22,15,11, 5,  4,  0};
	protected float  nightTemperatures[12]={-5,-4, -1, 6,9,15,15,10, 6, 4, -1, -5};
	
	protected float			m_TicksCount = 0; //ticks passed since last clothing wetting or drying
	protected float 		m_WetDelay = ENVIRO_TICKS_TO_WETNESS_CALCULATION; //koef compensating for delayed wetting processing

	//player
	protected PlayerBase 	m_Entity; 
	protected float			m_EntityHeightPositon; // y position of entity above water level (meters)
	protected float 		m_EntitySpeed; // 1-9 speed of entity movement
	protected float 		m_EntityTemperature; //34-44
	protected float 		m_EntityHeat; //3-9 heatcomfort generated by entites movement
	
	//environment
	protected float 		m_Rain = 0; // 0-1 amount of rain 
	protected float 		m_Wind = 0; // strength of wind
	protected float 		m_Fog = 0; // 0-1 how foggy it is
	protected float 		m_DayOrNight = 0; // 0-1 day(1) or night(0)
	protected float 		m_Clouds = 0; // 0-1 how cloudy it is
	protected float 		m_EnvironmentTemperature = 0; //temperature of environment entity is in
	protected int			m_CurrentMonth = 0; // 0-11 used fo proper air temperature selection
	protected float			m_Time = 0; //
	private const float 	ENVIRONMENT_TICK_RATE = 2;
		
	void Environment(PlayerBase player)
	{
		m_Entity = player;		
	}
			
	//ENTITY
	// Returns heat entity generates based on entitys movement speed (for now)
	float GetEntityHeat()
	{
		float heat = Math.Max(m_EntitySpeed,ENVIRO_DEFAULT_ENTITY_HEAT);
		return heat;
	}
	
	// Checks whether is entity sheltered
	bool IsUnderRoof()
	{
		vector player_postion = m_Entity.GetPosition();
		float player_y_postion = player_postion[1];
		vector from = m_Entity.GetPosition();   
		vector to =  from  + "0 25 0";
		//Shape.CreateArrow(from,to,1,3333,1);  
		vector contactPos;   
		vector contactDir;   
		int contactComponent;   
		return GetGame().Raycast(from, to, contactPos, contactDir, contactComponent);
	}
	
	// Calculates item heat isolation based on its wetness
	float GetCurrentItemHeatIsolation( ItemBase item )
	{
		float heat_isolation = g_Game.ConfigGetInt("cfgVehicles " + item.GetType() + " heatIsolation");
		float wet_factor = item.GetWet2();
		if ( wet_factor > 0 )
		{
			if ( wet_factor <= ENVIRO_WET_PENALTY )
			{
				heat_isolation = heat_isolation * (1 - wet_factor);
			}
			else
			{
				heat_isolation = -ENVIRO_WET_PENALTY_EFFECT * (wet_factor - ENVIRO_WET_PENALTY);
			}
		}
		return heat_isolation;
	}
	
	//ENVIRONTMENT
	// Returns amount of ?C air temperature should be lowered by based on entitys height above water level
	float GetTemperatureHeightCorrection()
	{
		float temperature_reduction = Math.Max(0, (m_EntityHeightPositon * ENVIRO_TEMPERATURE_HEIGHT_REDUCTION));
		return temperature_reduction;
	}
	
	// Calculates and return temperature of environment
	float GetEnvironmentTemperature()
	{
		float temperature;
		float night_temperature = nightTemperatures[m_CurrentMonth];
		float day_temperature = dayTemperatures[m_CurrentMonth];
		if ( night_temperature < day_temperature )
		{
			temperature = night_temperature + ((day_temperature-night_temperature)*m_DayOrNight);
		}
		else
		{
			temperature = day_temperature + ((night_temperature-day_temperature)*m_DayOrNight);
		}
		if ( m_Entity.IsWaterContact() ) temperature = temperature*ENVIRO_WATER_TEMPERATURE_KOEF;
		float fog_effect  = m_Fog*ENVIRO_FOG_TEMP_EFFECT;
		float clouds_effect = m_Clouds*ENVIRO_CLOUDS_TEMP_EFFECT;
		float combined_effect = clouds_effect+fog_effect;
		if ( combined_effect > 0 ) temperature = temperature*(1-combined_effect);
		temperature -= GetTemperatureHeightCorrection();
		return temperature;
	}	
		
	// Calculats wet/drying delta based on entitys location and weather
	float GetWetDelta() 
	{
		float wet_delta = 0;
		if ( m_Entity.IsWaterContact() )
		{
			//player is getting wet from swimming in water
			wet_delta = 1;
		}
		else
		{
			if ( m_Rain > 0 && !IsUnderRoof() )
			{	
				//player is getting wet from rain
				wet_delta = ENVIRO_WET_INCREMENT * m_WetDelay * (1+(ENVIRO_WIND_EFFECT*m_Wind)) * ( m_Rain );
			}
			else
			{
				//player is drying
				float sun_effect = (ENVIRO_SUN_INCREMENT * m_DayOrNight * (1-m_Fog))*(1-(m_Clouds*ENVIRO_CLOUD_DRY_EFFECT));
				wet_delta = -((ENVIRO_DRY_INCREMENT * m_WetDelay * Math.Sqrt(m_EntityHeat)) + sun_effect) * (1+(ENVIRO_WIND_EFFECT*m_Wind));
			}	
		}		
		return wet_delta;
	}
	

	// EXPOSURE
	// Each tick updates current entity member variables
	void CollectAndSetEntityData()
	{
		vector player_postion = m_Entity.GetPosition();
		m_EntityHeightPositon = player_postion[1];
		//(player_y_postion / 100)
		
		vector speed_vector = m_Entity.GetModelSpeed();
		vector zero_speed = "0 0 0"; 
		float agent_speed_distance = vector.Distance(zero_speed,speed_vector);
		float agent_speed = Math.Min(agent_speed_distance, 9);
		m_EntitySpeed = Math.Max(agent_speed, 1);
		m_EntityTemperature = 35; //can be current entity temeprature in future
		m_EntityHeat = GetEntityHeat();
	}
	
	// Each tick updates current environment member variables
	void CollectAndSetEnvironmentData()
	{
		m_Rain = g_Game.GetRain();
		m_DayOrNight = 0.5; // jtomasik - zjisti proc tohle vraci nulu misto 0-1 (-1*Math.AbsInt((1-(g_Game.GetDayTime()/ENVIRO_HIGH_NOON))))+1;
		m_Fog = g_Game.GetFog();
		m_Clouds =	g_Game.GetOvercast();
		m_CurrentMonth = 8; //can be set by server date in future
		m_Wind = g_Game.GetWindForce();
		m_EnvironmentTemperature = GetEnvironmentTemperature();
	}

	// Wets or dry items once in given time 
	void ProcessItemsWetness()
	{
		EntityAI attachment;	
		ItemBase item;
		int attachemnt_count = m_Entity.AttachmentsCount();
		for (int att = 0; att < attachemnt_count; att++)
		{	
			attachment = m_Entity.GetAttachmentFromIndex(att);
			if ( attachment.IsItemBase() )
			{
				item = (ItemBase)attachment;
				// Calculates temperature and process wetness for all items inside cargo
				if ( !item.KindOf("waterproof") )
				{
					if ( !item.KindOf("rainproof") ) item.AddWet2(GetWetDelta());
					if ( m_Entity.GetAttachmentFromIndex(att).GetCargo() )
					{
						int icount = m_Entity.GetAttachmentFromIndex(att).GetCargo().GetItemCount();
						for (int i = 0; i < icount; i++)
						{
							ItemBase in_item = attachment.GetCargo().GetItem(i);
							in_item.AddWet2(GetWetDelta()*ENVIRO_WET_PASSTHROUGH_KOEF);
						}
					}	
				}
			}
		}
	}

	// Calculates and process temperature of items
	void ProcessItemsHeat()
	{
		EntityAI attachment;	
		ItemBase item;
		float heat_comfort;  //delta that changes entitys temperature				
		float items_heat; // temperature of items in inventory modified by heat transfer koeficient
		float items_heat_isolation; // 0-30  isolation of clothes, fresh spawn heatisolation = 5.5 + 2.5 + 4a cca 12, cca 20 in sweater		
		int attachemnt_count = m_Entity.AttachmentsCount();
		for (int att = 0; att < attachemnt_count; att++)
		{	
			attachment = m_Entity.GetAttachmentFromIndex(att);
			if ( attachment.IsItemBase() )
			{
				item = (ItemBase)attachment;
				if ( item.IsClothing() ) items_heat_isolation += GetCurrentItemHeatIsolation(item);
				if ( m_Entity.GetAttachmentFromIndex(att).GetCargo() )
				{
					int icount = m_Entity.GetAttachmentFromIndex(att).GetCargo().GetItemCount();
					for (int i = 0; i < icount; i++)
					{
						ItemBase in_item = attachment.GetCargo().GetItem(i);
						items_heat += in_item.GetTemperature2();
					}
				}	
			}
		}
		// Calculate heat gained from items based on its temeprature
		items_heat = items_heat * ENVIRO_ITEM_HEAT_TRANSFER_KOEF;
		// Sets heatcomfort. Should be 0. More it is below 0 the colder player feels, more is it over 0 the hotter player feels.
		heat_comfort = ( m_EntityHeat + items_heat + items_heat_isolation - ( m_EntityTemperature - m_EnvironmentTemperature ) );	
		m_Entity.m_PlayerStats.m_HeatComfort.Set(heat_comfort);
		// Debug
		/*
		string debug_message = "Environment.c | ENTITY REPORT | heat comfort : " + ToString(heat_comfort) + " heat isolation: " + ToString(items_heat_isolation);
		m_Entity.MessageStatus(debug_message);
		Print(debug_message);			
		*/
	}
		
	// Calculates heatisolation of clothing, process its wetness, collects heat from heated items and calculates entitys heat comfort
	void Update(float delta_t)
	{		
		if (m_Entity)
		{  
			m_Time += delta_t;
			if ( m_Time >= ENVIRO_TICK_RATE )
			{
				m_Time = 0;
				m_TicksCount++; // Sets whether it is time to add wetness to items and clothing
						
				// Updates data
				CollectAndSetEntityData();
				CollectAndSetEnvironmentData();
				
				//Process wetness and temperature
				ProcessItemsHeat();				
				if ( m_TicksCount >= ENVIRO_TICKS_TO_WETNESS_CALCULATION )
				{	
					ProcessItemsWetness();
					m_TicksCount = 0;
				}
				//Debug
				/*
				debug_message = "Environment.c | WEATHER REPORT | environment temperature: " + ToString(m_EnvironmentTemperature) + " wind: " + ToString(m_Wind) + " rain: " + ToString(m_Rain) + " dayNight: " + ToString(m_DayOrNight) + " fog:" + ToString(m_Fog) + " clouds: " + ToString(m_Clouds) + " height: " + ToString(GetTemperatureHeightCorrection()) + " wetdelta: " + ToString(GetWetDelta(delta_t));
				m_Entity.MessageStatus(debug_message);
				Print(debug_message);
				*/
			}
		}
	}
};